/* Master Thai script GDL  */

#include <stddef.gdh>

//ScriptTag       = "THAI";
ScriptDirection = HORIZONTAL_LEFT_TO_RIGHT;
Bidi            = false;

table(feature)

bclr
{
    id="bclr";
    name.LG_USENG="Remove lower diacritic in yo ying, no nen";
    default = 0;
}

endtable;

table(language)

pali {
    languages = ("pli", "pi");
    bclr = 1;
};

endtable;


table (substitution)
pass(1)
    // Latin f(f?)[fil] ligatures
    g_f (g_f, g_i, g_l) > _ (g_ff, g_fi, g_fl):(1 2) / _ ^ _;
    g_ff (g_i, g_l)     > _ (g_ffi, g_ffl):(1 2);
    _ g0e33 > g0e4d g0e32 / _ cno_low? _ ;
    cno_low > clow / cnTakesAboveMarkDia _;
    cno_uline g0331 > culine:(1 3) _ / _ cBelowBaseDia? _;

if (bclr)
    cno_descless > cdescless;
else
    cno_descless > cdescless / _ cBelowBaseDia;
endif;

    cBelowBaseDia (g0331, g0e3a) > @3 @1 / _ cBelowBaseDia? _ ;
endpass;
endtable;

#ifndef opt2
#define opt(x)      [x]?
#define opt2(x)     [opt(x) x]?
#define opt3(x)     [opt2(x) x]?
#define opt4(x)     [opt3(x) x]?
#endif

table(positioning)
pass(1)
cTakesBelowBaseDia cBelowBaseDia {attach {to = @1; at = BelowBaseS; with = BelowBaseM}; user1 = 1} / _ ^ _{user1 == 0};
cTakesAboveBaseDia cAboveBaseDia {attach {to = @1; at = AboveBaseS; with = AboveBaseM}; user1 = 1} / ^ _ opt3(cBelowBaseDia) _ {user1 == 0};
cTakesBelowBase_MarkDia cBelowBase_MarkDia {attach {to = @1; at = BelowBase_MarkS; with = BelowBase_MarkM}; user1 = 1} / ^ _ opt3(cAboveBaseDia) _ {user1 == 0};
cTakesAboveMarkDia cAboveMarkDia {attach {to = @1; at = AboveMarkS; with = AboveMarkM}; user1 = 1} / ^ _ _ {user1 == 0};
endpass;
endtable;

